plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

minecraft {
    version(minecraft_version)
    runs {
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {
            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }

            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }

    accessWideners(
            file("src/main/resources/smallerunits.accesswidener")
    )
}

repositories {
    mavenCentral()
    maven {
        name = "cursemaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        // Flywheel
        name = 'tterrag maven'
        url = 'https://maven.tterrag.com'
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "rinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly project(":plat")
    compileOnly "curse.maven:embeddium-908741:5630140"
    compileOnly "curse.maven:alternate-current-548115:4721662"
    compileOnly "curse.maven:vivecraft-667903:5518169"
    compileOnly "curse.maven:oculus-581495:5299671"
    compileOnly "curse.maven:pehkui-319596:5393101"
    compileOnly "maven.modrinth:lithium:mc1.20.1-0.11.2"
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
}
